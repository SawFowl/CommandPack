plugins {
	id("java")
	id("eclipse")
	id("maven-publish")
}

group = "sawfowl.commandpack"
version = "${major}.${minor}.${patch}-${api}-${suffix}"

subprojects {
	apply {
		plugin 'java'
		plugin "maven-publish"
	}
	java {
		sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
	}
	repositories {
		mavenCentral()
		maven {
			name = "Sponge"
			url = "https://repo.spongepowered.org/repository/maven-public"
		}
		maven {
			name = "JitPack"
			url 'https://jitpack.io'
		}
	}
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path)}
task allJar(type: Jar, dependsOn: subprojects.assemble) {
	subprojects.each { subproject ->
		from subproject.configurations.archives.allArtifacts.files.collect {
			zipTree(it)
		}
	}
	manifest {
		attributes([
			"MixinConnector": "${project.group}.mixins.MixinConnector"
		])
	}
}

build.dependsOn(allJar);

jar.enabled(false)

afterEvaluate {
	publish.dependsOn allJar
}

publishing {
	publications {
		myLib(MavenPublication) {
			from project(":core").components.java
		}
	}
}

/*jar {
	subprojects.each { subproject ->
		from {
			subprojects.collect { it.sourceSets.main.output }
		}
	}
	manifest {
		attributes([
			"MixinConnector": "${project.group}.mixins.MixinConnector"
		])
	}
}*/

task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier = 'sources'
	from subprojects.collect { it.sourceSets.main.allSource }
	exclude('sawfowl/commandpack/apiclasses/**')
	exclude('sawfowl/commandpack/commands/**')
	exclude('sawfowl/commandpack/configure/**')
	exclude('sawfowl/commandpack/listeners/**')
	exclude('sawfowl/commandpack/utils/**')
	exclude('sawfowl/commandpack/CommandPack.java')
	exclude('sawfowl/commandpack/Permissions.java')
	exclude('sawfowl/commandpack/mixins/**')
	exclude('META-INF/sponge_plugins.json')
	exclude('*.json')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

configurations {
	doc {
		transitive false
	}
}

javadoc {
	options.encoding = 'UTF-8'
	source subprojects.collect { it.sourceSets.main.allJava }
	classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
	destinationDir = file("${buildDir}/docs/javadoc")
	include 'sawfowl/commandpack/api/*.java'
	include 'sawfowl/commandpack/api/commands/*.java'
	include 'sawfowl/commandpack/api/commands/parameterized/*.java'
	include 'sawfowl/commandpack/api/commands/raw/*.java'
	include 'sawfowl/commandpack/api/data/*.java'
	include 'sawfowl/commandpack/api/data/command/*.java'
	include 'sawfowl/commandpack/api/data/kits/*.java'
	include 'sawfowl/commandpack/api/data/miscellaneous/*.java'
	include 'sawfowl/commandpack/api/data/player/*.java'
	include 'sawfowl/commandpack/api/events/*.java'
	exclude('sawfowl/commandpack/mixins/**')
	exclude('META-INF/sponge_plugins.json')
	options.addStringOption('Xdoclint:none', '-quiet')
}

plugins {
	id("java")
	id("eclipse")
	id("idea")
	id("maven-publish")
}

group = "sawfowl.commandpack"
version = "${major}.${minor}.${patch}-${suffix}-S${api}"

subprojects {
	apply {
		plugin 'java'
	}
	java.toolchain.languageVersion = JavaLanguageVersion.of(21)
	repositories {
		mavenCentral()
		maven {
			name = "Sponge"
			url = "https://repo.spongepowered.org/repository/maven-public"
		}
		maven { 
			name = "NeoForge"
			url = 'https://maven.neoforged.net/releases'
		}
		maven { 
			name = "MinecraftLibs"
			url = 'https://libraries.minecraft.net/'
		}
		maven {
			name = "AspectRepo"
			url 'https://maven.aspect-realms.ru/repository/maven-public/'
		}
	}
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
		it.options.getRelease().set(21)
	}
	configurations {
		doc {
			transitive false
		}
	}
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path)}

//For use.
task allJar(type: Jar, dependsOn: subprojects.assemble) {
	subprojects.each { subproject ->
		from subproject.configurations.archives.allArtifacts.stream().filter {a -> a.file.name.contains("part")}.map {a -> a.file}.toList().collect{ zipTree(it) }
	}
	manifest {
		attributes([
			"MixinConnector": "${project.group}.mixins.MixinConnector"
		])
	}
}

build.dependsOn(allJar);

jar.enabled(false)

publishing {
	publications {
		myLib(MavenPublication) {
			from project(":core").components.java
			artifact project(":core").sourcesJar
			artifact project(":core").javadocJar
		}
	}
}
